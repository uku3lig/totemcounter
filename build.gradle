plugins {
    id 'fabric-loom' version '1.3-SNAPSHOT'
    id 'io.github.juuxel.loom-vineflower' version '1.11.0'
    id "io.freefair.lombok" version "8.3"
    id 'org.ajoberstar.grgit' version '5.0.0'
}

version = "${project.mod_version}+mc${project.minecraft_version}${grgit.status().clean ? "" : "-dirty"}"
group = project.maven_group

repositories {
    maven {
        url "https://maven.uku3lig.net/releases"
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation(fabricApi.module("fabric-resource-loader-v0", project.fabric_version))
    modImplementation(fabricApi.module("fabric-key-binding-api-v1", project.fabric_version))
    modImplementation(fabricApi.module("fabric-command-api-v2", project.fabric_version))

    // TODO REMOVE
    modRuntimeOnly(fabricApi.module("fabric-screen-api-v1", project.fabric_version))

    modApi("net.uku3lig:ukulib:${project.ukulib_version}")
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}
