plugins {
    id 'org.quiltmc.loom' version '1.+'
    id 'io.github.juuxel.loom-quiltflower' version '1.8.+'
    id "io.freefair.lombok" version "6.6.+"
}

version = "${project.mod_version}+mc${project.minecraft_version}"
group = project.maven_group

repositories {
    maven {
        url "https://maven.uku3lig.net/releases"
    }
    maven {
        url = "https://api.modrinth.com/maven"
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "org.quiltmc:quilt-mappings:${project.quilt_mappings}:intermediary-v2"
    modImplementation "org.quiltmc:quilt-loader:${project.loader_version}"

    modImplementation "org.quiltmc.quilted-fabric-api:fabric-key-binding-api-v1:${project.qfapi_version}"
    modImplementation "org.quiltmc.qsl:core:${qsl_version}"
    modApi("net.uku3lig:ukulib:${project.ukulib_version}")

    modRuntimeOnly "maven.modrinth:lazydfu:0.1.3"
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}
